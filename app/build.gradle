plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.xdust.auryxbrowser'
    compileSdk 34

    defaultConfig {
        applicationId "com.xdust.auryxbrowser"
        minSdk 21
        targetSdk 34
        versionCode 225001
        versionName "1.226.01-beta"
    }

    signingConfigs {
        release {
            def keystorePassword = System.getenv("KEYSTORE_PASSWORD")
            def keyAliasEnv = System.getenv("KEY_ALIAS")
            def keyPasswordEnv = System.getenv("KEY_PASSWORD")

            if (keystorePassword != null &&
                keyAliasEnv != null &&
                keyPasswordEnv != null) {

                storeFile file("$rootDir/release.keystore")
                storePassword keystorePassword
                keyAlias keyAliasEnv
                keyPassword keyPasswordEnv
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
        }
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
}
